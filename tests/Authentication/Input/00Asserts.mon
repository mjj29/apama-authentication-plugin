event Asserts
{
	static action equalsWithin(any a, any b, float tol) returns boolean
	{
		switch (a) {
			case integer: {
				return a = <integer> b;
			}
			case string: {
				return a = <string> b;
			}
			case boolean: {
				return a = <boolean> b;
			}
			case float: {
				if a < (<float> b)-tol or a > (<float> b)+tol {
					return false;
				}
			}
			default: {
				return a.toString() = b.toString();
			}
		}
		return true;
	}

	static action assertThrows(string name, any #action, sequence<any> args, string type)
	{
		action<sequence<any> > returns any act := #action.getGenericAction();
		try {
			any _ := act(args);
			log "assert fail "+name+": action did not throw" at ERROR;
		} catch (com.apama.exceptions.Exception e) {
			log name+" caught expected exception "+e.toString() at INFO;
			assertEquals(name, type, e.getType());
		}
	}
	
	static action assertEquals(string name, any a, any b)
	{
		if not equalsWithin(a, b, 0.0001) {
			log "assert fail "+name+": "+a.valueToString()+" != "+b.valueToString() at ERROR;
		}
	}
	static action assert(string name, boolean a)
	{
		if not a {
			log "assert fail "+name at ERROR;
		}
	}
}
